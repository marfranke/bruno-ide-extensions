{
  parserClass="com.usebruno.plugin.bruno.language.parser.BruParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Bru"
  psiImplClassSuffix="Impl"
  psiPackage="com.usebruno.plugin.bruno.language.psi"
  psiImplPackage="com.usebruno.plugin.bruno.language.psi.impl"

  elementTypeHolderClass="com.usebruno.plugin.bruno.language.psi.BruTypes"
  elementTypeClass="com.usebruno.plugin.bruno.language.psi.BruElementType"
  tokenTypeClass="com.usebruno.plugin.bruno.language.psi.BruTokenType"
}

bruFile ::= tag*

tag ::= (dictionaryTag|textTag)
dictionaryTag ::= (TAG_META|TAG_HTTP|TAG_HEADERS|TAG_QUERY|TAG_VARS|TAG_AUTH|TAG_ASSERT|TAG_BODY_DICTIONARY) BLOCK_BEGIN dictionary BLOCK_END
textTag ::= (TAG_BODY_TEXT|TAG_SCRIPT|TAG_TEST|TAG_DOCS) BLOCK_BEGIN textBlock BLOCK_END

dictionary ::= dictionaryEntry*
dictionaryEntry ::= DICTIONARY_KEY SEPARATOR DICTIONARY_VALUE

textBlock ::= TEXT_LINE* {
  extends ="com.usebruno.plugin.bruno.language.injection.TextBlockLanguageInjectionHostMixin"
}
